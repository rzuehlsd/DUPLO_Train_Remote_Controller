

/**
 * Steuern von zwei Duplo Zügen mit einer ESP32 Steuerkonsole
 * 
 * Umschalten Zwischen den Zügen über einer Taster und Rückmeldung über
 * LED (LED aus = Zug 1, LED an = Zug 2)
 * 
 * Steuersignale des Steuerpultes werden an den aktuell ausgewählten Zug
 * gesendet.
 * 
 * Pin Belegung am ESP32 WROOM32 LiOn 18650 Modul:
 * 
 * GPIO Pin     Funktion
 *  12          Taster 5 (Zugumschaltung). nicht verbaut
 *  13          LED (Anzeige gewählter Zug), nicht verbaut
 *  14          Taster 4 (rot)
 *  15          Poti Fahrtregler (Analogeingang ADC13)
 *  25          Taster 3 (blau)
 *  26          Taster 2 (weiß)
 *  27          Taster 1 (gelb)
 * 
 */


#include "Lpf2Hub.h"      //legoino
#include <Bounce2.h>      //bounce2



// byte port = (byte)PoweredUpHubPort::A;  wird nicht verwendet!!



//Pin declaration
#define BTN_MUSIC 25        // Taster 3 (blau)           
#define BTN_LICHT 26        // Taster 2 (weiß)
#define BTN_WASSER 27       // Taster 1 (gelb)
#define BTN_ZUG 12          // Taster 5 (Zugumschaltung)
#define LED_ZUG 13          // LED (Anzeige gewählter Zug)
#define BTN_STOP 14         // Taster 4 (rot)
#define POTI_SPEED 15       // Poti Fahrtregler (Analogeingang ADC13)


// Hier werden zwei Züge gesteuert
#define NO_TRAINS 2
Lpf2Hub myTrainHub[NO_TRAINS];

#define DELAY_TIME 50       // delay in ms


// Debouncing und Handling der Buttons
Bounce pbMusic = Bounce();
Bounce pbLight = Bounce();
Bounce pbWater = Bounce();
Bounce pbStop = Bounce();
Bounce pbZug = Bounce();



// lokale Statusvariablen für beide Züge
static Color gColor[NO_TRAINS] = {BLACK, BLACK};
static int gSpeed[NO_TRAINS] = {0, 0};

static int selected = 0;        // selektiert den zu steuernden Zug


byte motorPort = (byte)DuploTrainHubPort::MOTOR;    // Port des Zugmotors 


// setzt die Geschwindigkeit des aktuell selektierten Zuges
void handlePoti()
{
  int speed = 0;
  int potiSpeed = analogRead(POTI_SPEED);

  Serial.print("Poti = ");Serial.println(potiSpeed);

  // Mapping des ADC Wertes des Poties auf die Zuggeschwindigkeit
  // ADC-Wert bei analogRead() ist 12 bit (0 - 4096)
  // Remapping des Wertebereiches auf -64 bis +64

  speed = map(potiSpeed, 0, 4096, -4, 4) * 16;

  Serial.print("Speed = ");Serial.println(speed);


  // Poti Input entspricht nicht der aktuellen Speed des selektierten Zuges
  if(speed != gSpeed[selected])
  { 
    if(gSpeed[selected] == 0 && speed != 0)
    {
        // Zug fährt an (vorwärts oder rückwärts)
        myTrainHub[selected].playSound((byte)DuploTrainBaseSound::STATION_DEPARTURE);
        delay(DELAY_TIME);
    }
    if(gSpeed[selected] != 0 && speed == 0)
    {
        // Zug fährt vorwärts oder rückwärts und kommt zum Stehen
        myTrainHub[selected].playSound((byte)DuploTrainBaseSound::BRAKE);
        delay(DELAY_TIME);
    }

    // Zug verändert die Geschwindigkeit
    gSpeed[selected] = speed;
    myTrainHub[selected].setBasicMotorSpeed(motorPort, speed);
  }
}


inline Color getNextColor(Color col)
{
    // ColorMap hat 10 verschiedene Werte 
    // Inkrementieren dann Integer Division mit Rest auf gColor zuweisen
    return (Color) (((int)col +1) % NUM_COLORS);
} 



// Buttons werden in der MainLoop aktualisiert
void handleButtons()
{
  if(pbZug.update())
  {
    if(pbZug.fell())
    {
      selected = (selected + 1) % NO_TRAINS;     // wie bei Color; selectiert einen der beiden Züge
      myTrainHub[selected].playSound((byte)DuploTrainBaseSound::HORN);
      delay(DELAY_TIME);
      myTrainHub[selected].playSound((byte)DuploTrainBaseSound::HORN);
      delay(DELAY_TIME);
    }
  }
  if(pbMusic.update())
  {
    if(pbMusic.fell())
    {
      myTrainHub[selected].playSound((byte)DuploTrainBaseSound::HORN);
      delay(DELAY_TIME);
    }
  }
  if(pbLight.update())
  {
    if(pbLight.fell())
    {
      gColor[selected] = getNextColor(gColor[selected]);
      myTrainHub[selected].setLedColor(gColor[selected]);
      delay(DELAY_TIME);
    }
  }
  if(pbWater.update())
  {
    if(pbWater.fell())
    {
      myTrainHub[selected].playSound((byte)DuploTrainBaseSound::WATER_REFILL);
      delay(DELAY_TIME);  
    }
  }
  if(pbStop.update())
  {
    if(pbStop.fell())
    {
      myTrainHub[selected].setBasicMotorSpeed(motorPort, 0);
      delay(DELAY_TIME);
      myTrainHub[selected].playSound((byte)DuploTrainBaseSound::BRAKE);
    }
  }
}



// Callback des Farbsensors um die Kommandos der Farbsteine zu berücksichtigen
void colorSensorCallback(void *hub, byte portNumber, DeviceType deviceType, uint8_t *pData)
{
  Lpf2Hub *myHub = (Lpf2Hub *)hub;

  if (deviceType == DeviceType::DUPLO_TRAIN_BASE_COLOR_SENSOR)
  {
    int color = myHub->parseColor(pData);
    Serial.print("Color: "); Serial.println(COLOR_STRING[color]);

    // LED auf erkannte Farbe setzen
    myHub->setLedColor((Color)color);

    if (color == (byte)RED)
    {
      myHub->playSound((byte)DuploTrainBaseSound::BRAKE);
      myHub->stopBasicMotor(motorPort);
    }
    else if (color == (byte)BLUE)
    {
      myHub->playSound((byte)DuploTrainBaseSound::BRAKE);
      myHub->stopBasicMotor(motorPort);
      myHub->playSound((byte)DuploTrainBaseSound::WATER_REFILL);
    }
    else if (color == (byte)YELLOW)
    {
      myHub->playSound((byte)DuploTrainBaseSound::HORN);
    }
  }
}




// Initialisierung der Hubs und initiieren der BLE Suche
// wird auch für Reconnect der Züge genutzt
void initTrains()
{
  for(int i = 0; i < NO_TRAINS; i++)
  {
    if (!myTrainHub[i].isConnected()) 
    {
      myTrainHub[i].init();
      Serial.print("Zug No. "); Serial.print(i); Serial.println(" initialisiert!");
      delay(DELAY_TIME);
    } 
  }
}



// wird kontinuierlich in der Main Loop ausgeführt
// um Züge zu initialisieren und zu verbinden
// Verbundene Züge melden sich akustisch
void handleTrains()
{
  for(int i = 0; i < NO_TRAINS; i++)
  {
    // Züge versuchen sich zu verbinden
    if (myTrainHub[i].isConnecting()) 
    {
      // Nur Duplo Züge verbinden
      if (myTrainHub[i].getHubType() == HubType::DUPLO_TRAIN_HUB)
      {
        // Zug verbinden und auswählen
        myTrainHub[i].connectHub();
        selected = i;  
    
        Serial.print("Zug No. "); Serial.print(i); Serial.println(" verbunden!");
      
        // Color Sensor aktivieren um auch die Kommandos der Farbsteine zu erkennen
        myTrainHub[i].activatePortDevice((byte)DuploTrainHubPort::COLOR, colorSensorCallback);

        // Zug meldet sich nach verbinden
        myTrainHub[i].setLedColor(getNextColor(gColor[i]));
        myTrainHub[i].playSound((byte)DuploTrainBaseSound::HORN);
        delay(DELAY_TIME);
      }
    }
  }

  // Initialisieren für Reconnect, falls Züge nicht mehr verbunden sind
  initTrains();

}



void setup() {
  Serial.begin(115200);
  Serial.println("Initialisieren von Train Remote Control für 2 Züge");

  //PushButtons initialisieren
  pbMusic.attach(BTN_MUSIC, INPUT_PULLUP);
  pbLight.attach(BTN_LICHT, INPUT_PULLUP);
  pbWater.attach(BTN_WASSER, INPUT_PULLUP);
  pbStop.attach(BTN_STOP, INPUT_PULLUP);


  // Erstmalige Initialisierung der Hubs der Züge 
  // Dadurch wird die Suche nach Lego Hubs angestoßen
  initTrains();
}




void loop() 
{
  handleTrains();

  handleButtons();

  handlePoti();
}
